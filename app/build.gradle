buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

String APK_ID = "com.cheatdatabase"
String APK_NAME = "Cheat-Database"
int VERSION_CODE = 32
String VERSION_NAME = "2.1.9"

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'
    defaultConfig {
        applicationId APK_ID
        archivesBaseName = APK_NAME
        minSdkVersion 19
        targetSdkVersion 27
        versionCode VERSION_CODE
        versionName VERSION_NAME
        multiDexEnabled true
    }

    dexOptions {
        javaMaxHeapSize "4g" //specify the heap size for the dex process
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    // Specifies a flavor dimension.
    flavorDimensions "myDimension"

    productFlavors {
        googlePlayFlavor {
            dimension "myDimension"
            versionNameSuffix "-googlePlay"
        }
        amazonFlavor {
            dimension "myDimension"
            versionNameSuffix "-amazon"
        }
    }

    lintOptions {
        abortOnError false
        disable 'MissingTranslation'
    }
}

repositories {
    maven { url "https://dl.bintray.com/drummer-aidan/maven/" }
    maven { url 'http://swisscodemonkeys.github.io/appbrain-sdk/maven' }
    maven { url "https://jitpack.io" }
    maven { url 'https://maven.fabric.io/public' }
    maven { url 'https://s3.amazonaws.com/moat-sdk-builds' }
    maven { url 'https://maven.google.com' }
    maven { url 'https://jitpack.io' }
    maven { url 'https://adcolony.bintray.com/AdColony' }
    maven { url 'https://tapjoy.bintray.com/maven' }
    jcenter()
    mavenCentral()
}

ext {
    supportVersion = '27.1.1'
    playServicesVersion = '15.0.0'
    AAVersion = '4.4.0'
}

dependencies {
    implementation "com.google.firebase:firebase-core:$playServicesVersion"
    implementation "com.google.firebase:firebase-invites:$playServicesVersion"
    implementation "com.google.firebase:firebase-config:$playServicesVersion"
    implementation "com.google.firebase:firebase-crash:$playServicesVersion"
    implementation "com.google.firebase:firebase-messaging:$playServicesVersion"
    implementation "com.google.firebase:firebase-appindexing:$playServicesVersion"
    api "com.android.support:appcompat-v7:$supportVersion"
    api "com.android.support:design:$supportVersion"
    api "com.android.support:recyclerview-v7:$supportVersion"
    implementation "com.github.vajro:MaterialDesignLibrary:1.6"
    implementation "com.afollestad.material-dialogs:core:0.9.6.0"
    implementation "com.google.code.gson:gson:2.8.5"
    implementation "com.google.android.gms:play-services-analytics:$playServicesVersion"
    implementation "com.google.android.gms:play-services-auth:$playServicesVersion"
    implementation "com.squareup.picasso:picasso:2.71828"
    implementation "com.appbrain:appbrain-applift-sdk:+"
    implementation "org.greenrobot:eventbus:3.1.1"
    annotationProcessor "org.androidannotations:androidannotations:$AAVersion"
    annotationProcessor "org.androidannotations:ormlite:$AAVersion"
    implementation "org.androidannotations:androidannotations-api:$AAVersion"
    implementation("com.mopub:mopub-sdk:5.3.0@aar") {
        transitive = true
        // Exclude this to make it work with Gradle 3 / Android Studio 3
        exclude module: "libAvid-mopub" // To exclude AVID
        exclude module: "moat-mobile-app-kit" // To exclude Moat
    }

    implementation "com.github.hackware1993:MagicIndicator:1.5.0"
    implementation "com.simplecityapps:recyclerview-fastscroll:1.0.18"
    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true
    }

    // Unity Ads
    // This mediated network SDK is not available on JCenter. Ensure you have downloaded and imported it manually into your app project.
    implementation 'com.mopub.mediation:unityads:2.3.0.1'

    // Vungle
    implementation 'com.github.vungle:vungle-android-sdk:6.3.17'
    implementation 'com.mopub.mediation:vungle:6.3.17.1'

    // AdColony
    implementation 'com.adcolony:sdk:3.3.5'
    implementation 'com.mopub.mediation:adcolony:3.3.5.1'

    // Facebook Audience Network
    implementation 'com.facebook.android:audience-network-sdk:4.99.1'
    implementation 'com.mopub.mediation:facebookaudiencenetwork:4.99.1.3'

    // Tapjoy
    implementation 'com.tapjoy:tapjoy-android-sdk:12.0.0@aar'
    implementation 'com.mopub.mediation:tapjoy:12.0.0.1'
}

apply plugin: "com.google.gms.google-services"
